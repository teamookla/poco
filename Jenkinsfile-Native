import java.util.regex.Pattern

def OPENSSL_VERSION = "1.1.1c"

def agents = [
  deb5_32bit:    [ label: 'native-deb5-64', toolchains: [
    'gcc-6.3-deb5-multi.tar.gz': '/home/jenkins/toolchains',
    "openssl-${OPENSSL_VERSION}-deb5_32bit.tar.gz": "."
  ] ],
  deb5_64bit:    [ label: 'native-deb5-64', toolchains: [
    'gcc-6.3-x86_64_deb5-64.tgz': '/home/jenkins/toolchains',
    "openssl-${OPENSSL_VERSION}-deb5_64bit.tar.gz": "."
  ] ],
  deb8_64bit:    [ label: 'native-deb8-64', toolchains: [
    "openssl-${OPENSSL_VERSION}-deb8_64bit.tar.gz": "."
  ] ],
  deb9_64bit:    [ label: 'native-deb9-64', toolchains: [
    "openssl-${OPENSSL_VERSION}-deb9_64bit.tar.gz": "."
  ] ],
  freebsd64:     [ label: 'freebsd64-11', toolchains: [
    "openssl-${OPENSSL_VERSION}-freebsd11_64bit.tar.gz": "."
  ] ],
  freebsd12_64:     [ label: 'freebsd64-12', toolchains: [
    "openssl-${OPENSSL_VERSION}-freebsd12_64bit.tar.gz": "."
  ] ],
  macos:         [ label: 'macos', toolchains: [
    "openssl-${OPENSSL_VERSION}-macos.tar.gz": "."
  ] ],
  win32:         [ label: 'windows-build-pool', toolchains: [
    "openssl-${OPENSSL_VERSION}-win32.tar.gz": "."
  ] ],
  win64:         [ label: 'windows-build-pool', toolchains: [
    "openssl-${OPENSSL_VERSION}-win64.tar.gz": "."
  ] ],
]

def builds = [
  'linux32':        [ agent: agents.deb5_32bit ],
  'linux64':        [ agent: agents.deb5_64bit ],
  'linux64-deb8':   [ agent: agents.deb8_64bit ],
  'linux64-deb9':   [ agent: agents.deb9_64bit ],
  'freebsd64':      [ agent: agents.freebsd64 ],
  'freebsd12_64':   [ agent: agents.freebsd12_64 ],
  'macosx':         [ agent: agents.macos ],
  'windows32':      [ agent: agents.win32, winPlatform:'x86' ],
  'windows64':      [ agent: agents.win64, winPlatform:'x64' ],
]

def createBuildStages(builds) {
  builds.collectEntries { name, definition ->
    [ "${name}": createBuildStage(name, definition) ]
  }
}

def createBuildStage(name, definition) {
  return {
    stage(name) {

      node(definition.agent.label) {
        ws("workspace/" + generateWorkspaceName("${env.APPLICATION_NAME}-${env.BRANCH_NAME}-${name}")) {
          checkout scm
          withEnv([
            "PLATFORM=${definition.platform ?: name}",
          ]) {
            withAwsCredentials('jenkins-native-builds') {
              if (definition.agent.toolchains) {
                definition.agent.toolchains.each { toolchain, dest ->
                  if (toolchain.endsWith('.zip')) {
                    sh """
                      aws s3 cp s3://ookla-native-toolchains/${toolchain} ${toolchain}
                      [ -e ${dest} ] || mkdir -p ${dest}
                      unzip -o ${toolchain} -d ${dest}
                      rm ${toolchain}
                    """
                  } else {
                    sh """
                      aws s3 cp --no-progress s3://ookla-native-toolchains/${toolchain} ${toolchain}
                      [ -e ${dest} ] || mkdir -p ${dest}
                      tar xzf ${toolchain} -C ${dest}
                      rm ${toolchain}
                    """
                  }
                }
              }

              ['Debug', 'Release'].each { buildType ->
                dir("cmake_build_${buildType}") {
                  deleteDir()
                }
                dir("cmake_install_${buildType}") {
                  deleteDir()
                }
              }

              if (isUnix()) {
                sh 'PATH=$PATH:/opt/cmake-3.11.4/bin exec ./jenkins.sh'
              } else {
                msvcBat "set PLATFORM=${definition.platform ?: name} & sh ./jenkins.sh", [winPlatform: definition.winPlatform]
              }

              def version = env.BRANCH_NAME
              def m = Pattern.compile("${APPLICATION_NAME}-(\\d+.\\d+.\\d+)").matcher(env.BRANCH_NAME)
              if (m.matches()) {
                version = m.getAt(0)[1]
              }
              m = null

              ['Debug', 'Release'].each { buildType ->
                sh "rm -f cmake_install_${buildType}.zip"
                zip zipFile:"cmake_install_${buildType}.zip", dir:"cmake_install_${buildType}"
                s3Upload(
                  bucket:'ookla-native-artifacts',
                  path:"${APPLICATION_NAME}/${env.BRANCH_NAME}/${APPLICATION_NAME}-${version}-${PLATFORM}-${buildType}.zip",
                  file:"cmake_install_${buildType}.zip")
              }
            }
          }
        }
      }
    }
  }
}

pipeline {
  agent none
  environment {
    APPLICATION_NAME = "poco"
    BUILD_TIMESTAMP = "${timeStamp}"
    OPENSSL_VERSION = "${OPENSSL_VERSION}"
  }
  stages {
    stage('Build All') {
      steps {
        script {
          throttle(['fargate']) {
            parallel createBuildStages(builds)
          }
        }
      }
    }
  }
}
